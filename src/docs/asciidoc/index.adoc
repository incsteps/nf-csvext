= nf-csvext
Jorge Aguilera <jorge@incsteps.com>
:toc: left
:imagesdir: images

== Install

To install the plugin, simply add the plugin into your `nextflow.config`

.nextflow.config
[source]
----
plugins {
    id "nf-csvext@0.1.0"
}
----

Once installed the plugin povides several csv `functions`

== General specifications

Most of the functions will follow the `csv_xxxx` pattern, where `xxxx` will describe the intent of the
function as for example `csv_sort` will sort a CSV by an specified column

Functions will accept a `file` as first argument as the csv `source` , except creation functions

Most functions will accept an optional `Map` as last arguments. In this map you can provide details
about the operacion. For example, you can provide the `sep` entry to specify the separator, or
`header` to indicate if the header need to be processed

In almost all situations, functions needs to store files in a temporary directory. For performance reasons, this directory is located in the machine’s local storage, and it should have enough free space. The tempDir option can be used to specify a different temporary directory.

== csv_concat

Concat two (or more) CSV files.

If a header is specified, it validates that files have the same size. Appended file(s) will not output
their header

=== Example

[source]
----
include { csv_concat } from 'plugin/nf-csvext'

params.input = 'data/names.csv'
params.concat = 'data/concat_names.csv'
csv_file = file(params.input)

workflow {

    Channel.fromPath( params.input)
        | map{ source ->
            csv_concat( source, file(params.concat), header:true )
        }
        | splitCsv(header:true)
        | view
}
----

if `params.input` and `params.concat are both a csv with `name`, `birthdate`, `quote`
, with one line everyone, them the output will be:

[source]
----
[name:Albert Einstein, birtdhay:14 de marzo de 1879, quote:"La imaginación es más importante que el conocimiento."]
[name:Rosa Parks, birthday:4 de febrero de 1913, quote:"Cada persona debe vivir su vida como un modelo para los demás."]
----

=== File collection

To append more than one file to `source` use a List as second arguments:

`csv_concat( source, [ file(params.concat), file(params.another_file) ], header:true )`

== csv_sort

Sort a CSV by a column index (or name) and return a new file

[source]
----
workflow {
   Channel.fromPath( params.input )
        | map{ source ->
            csv_sort( source, column: 2) //<1>
        }
        | splitCsv(header:true)
        | view
}
----
<1> Column can be an integer or a String. In this case, must be a column name present in the headers

== csv_trim

Remove one or more columns from the CSV and return a new CSV

[source]
----
include { csv_trim } from 'plugin/nf-csvext'

params.trim = 'Cabin,Pclass'

workflow {
    Channel.fromPath( 'https://raw.githubusercontent.com/incsteps/nf-csvext/refs/heads/main/validation/data/titanic.tsv' )
        | map{ source ->
            csv_trim( source, columns:params.trim, sep:'\t')
        }
        | splitCsv(header:true, sep:'\t')
        | view
}
----
<1> Use `column` to remove a single column

== csv_create (IN PROGRESS)

A `csv_create` operator is provided similar to `collectFiles`. Once configured, every item received in the channel
can be transformed and them the operator will emit a CSV

[source]
----
include { csv_create } from 'plugin/nf-csvext'

process foo {
  input:
  path x
  output:
  stdout
  script:
  """
  echo foo;$x
  """
}

workflow {
  def ch = Channel.fromPath("$baseDir/data/*")
  foo(ch)
    .csv_create( [headers:'name,surname', sep:';'], { line->
        line.toUpperCase()
    })
    | view
}
----

